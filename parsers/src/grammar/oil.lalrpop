use tok::{self, Tok};
use std::str::FromStr;

grammar<'input>;

pub ui_package: () = {
    import* template_or_style_or_view* => (),
};

import: () = {
    "import" components "from" path ";" => (),
    "import" path ";" => (),
};

components: () = {
    "*" => (),
    "{" (component ",")* component? "}" => (),
    component => (), // Can only be $img or $font
};

component: () = ident => ();

template_or_style_or_view: () = {
    template,
    style,
    view,
};

path: () = {
    "StringLiteral" => (),
};

ident: () = "Id" => ();
// ident: () = <r"[a-zA-Z_\\-][0-9a-zA-Z_\\-]*">
//     => ();

//number: f32 = <r"[-+]?([0-9]*[\.])?[0-9]*(e[-+][0-9][0-9])?">
//    => f32::from_str(<>).unwrap();

// =================================
//          Templates
//

template: () = {
    export? "template" ident template_args? trigg_events? "=" node ";" => (),
};

export: () = "export" => ();

template_args: () = {
    template_arg,
    "[" (template_arg ",")* template_arg? "]" => (),
};

template_arg: () = ident => ();

trigg_events: () = {
    "->" events => (),
};

events: () = {
    event,
    "(" (event ",")* event? ")" => (),
};

event: () = ident => ();

node: () = {
    "<" ident class? node_args* node_events* ">" node* "</" ident ">" => (),
};

class: () = ident property_value => ();

node_args: () = {
    "[" ident "]" node_value => (),
};

node_events: () = {
    "(" ident ")" node_value => (),
};

node_value: () = {
    "=" "StringLiteral" => (),
    property_value => (),
};

property_value: () = {
    "=" "{" property "}" => (),
};

property: () = {
    ident => (),
    property "DotId" => (),
};

// =================================
//          Styles
//

style: () = {
    export? "DotId" "{" style_properties* "}" => (),
};

style_properties: () = {
    // "background-image" ":" "$img" => (),
    // "font" ":" "$font" => (),
    ident ":" style_value => (),
};

style_value: () = {
    "{" ident "}" => (),
    "Number" "Id" ";" => (),
    ident ";" => (),
};

// =================================
//          View
//

view: () = {
    "view" "ViewId" view_args "=" "{" node* "}" => (),
};

view_args: () = {
    "(" ident "," ident ")" => (),
};

// =================================
//          Tokens
//
extern {
    type Location = usize;
    type Error = tok::Error;
    enum Tok<'input> {
        "import" => Tok::Import,
        "from" => Tok::From,
        "view" => Tok::View,
        "template" => Tok::Template,
        "export" => Tok::Export,

        // Special keywords accompanied by a series of
        // uninterpreted strings representing path
        "StringLiteral" => Tok::StringLiteral(<&'input str>),
        "Number" => Tok::Number(<&'input str>), // No unit

        "TextNode" => Tok::TextNode(<&'input str>), // Includes binded properties

        // Identifiers of various kinds:
        "DotId" => Tok::DotId(<&'input str>), // excludes the '.'
        "ViewId" => Tok::ViewId(<&'input str>), // excludes the '!'
        "Id" => Tok::Id(<&'input str>),

        "->" => Tok::Arrow,

        "{" => Tok::LeftBrace,
        "}" => Tok::RightBrace,
        "[" => Tok::LeftBracket,
        "]" => Tok::RightBracket,
        "(" => Tok::LeftParen,
        ")" => Tok::RightParen,
        "<" => Tok::LessThan,
        "</" => Tok::LessThanSlash,
        ">" => Tok::GreaterThan,
        //"#" => Tok::Hash,
        //"?" => Tok::Question,
        "=" => Tok::Equals,
        "+" => Tok::Plus,
        "-" => Tok::Minus,
        ";" => Tok::Semi,
        "*" => Tok::Star,
        ":" => Tok::Colon,
        "," => Tok::Comma,
    }
}
